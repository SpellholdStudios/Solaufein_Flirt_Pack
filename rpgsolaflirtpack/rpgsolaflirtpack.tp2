// Version 1.1
BACKUP ~rpgsolaflirtpack/backup~
AUTHOR ~JolythA@hotmail.com~

VERSION ~v1.1~

README ~rpgsolaflirtpack/README-RPGSolaFlirtPack.txt~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

		// Determines which game is installed and if the game is an Enhanced Edition.
		OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0						// eet


		/* eet compatibility */
		ACTION_IF (is_eet) BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END


		/* ---------------------------------------------------------------------------------------------- *
		 *    Convert strings to UTF-8 for BGEE/BG2EE                                                     *
		 *    No exception, everything needs to be converted                                              *
		 * -----------------------------------------------------------------------*---------------------- */
		ACTION_DEFINE_ARRAY rpg#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY rpg#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = rpg#noconvert
				reload_array = rpg#reload
		END

	END

END


AUTO_TRA ~rpgsolaflirtpack/lang/%s~

LANGUAGE ~English~
         ~english~
		 ~rpgsolaflirtpack/lang/english/setup.tra~


/* =========================================================== *
 *  The RPG Dungeon Solaufein Flirt Pack for BG2: SoA and ToB  *
 * =========================================================== */
BEGIN @0
DESIGNATED 0 LABEL ~solaufein_flirt_pack~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @1			// ~You must have BG2, ToB, BGT, BG2:EE or EET installed to install the RPG Solaufein Flirt Pack for BG2.~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~setup-solaufein.tp2~ ~0~ @2	// ~This mod requires the Weimer's Solaufein Romance Mod to be installed.~

ACTION_IF FILE_EXISTS ~override/rpgsolaflirtpack.rpgd~ BEGIN
	FAIL @2
END ELSE BEGIN
	COPY_EXISTING ~sw1h01.itm~ ~override/rpgsolaflirtpack.rpgd~
END

/* STATE.IDS patching to ToB - thanks, Cam, if you read it */
  /* adds custom IsValidForPartyDialogue state */
  APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

/*
// Gwendolyne's Workaround to fix the no-initiating flirt options when clicking on Solaufein for the first time. Not tested in game.
// Replaces the old code that was not patching anything:
// ---------------------------------------------------------------------------------------------------------------------------------
COPY_EXISTING ~sola.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY "IF ~True()~ \(THEN BEGIN 130\)" "IF ~False()~ \1"
	END
*/
OUTER_SET SolaState130 = STATE_WHICH_SAYS 229 IN ~solarom/%s/sola.tra~ FROM SOLA
ACTION_IF SolaState130 >= 0 BEGIN	// Compiles only in case STATE_WHICH_SAYS throws a valid value
	<<<<<<<< rpgsolaflirtpack/alter_sola.d
REPLACE_STATE_TRIGGER sola %SolaState130% ~False()~
>>>>>>>>
	COMPILE ~rpgsolaflirtpack/alter_sola.d~ EVALUATE_BUFFER
END

COMPILE EVALUATE_BUFFER ~rpgsolaflirtpack/files/rpgsolaflirts.d~

EXTEND_TOP ~sola.bcs~ ~rpgsolaflirtpack/files/rpgsola.baf~ EVALUATE_BUFFER 

// EOF: RPG Solaufein Flirt Pack for BG2: SoA and ToB